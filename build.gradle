plugins {
    id 'org.example.configurer-gradle-plugin' version '1.0'
}

group 'org.example'
version '1.0-SNAPSHOT'

sourceCompatibility = '11'
compileJava.options.encoding = 'UTF-8'

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    implementation(
            'org.springframework.boot:spring-boot-starter-web',
            'org.springframework.boot:spring-boot-starter-data-jpa',
            'org.postgresql:postgresql'
    )

    testImplementation(
            'org.springframework.boot:spring-boot-starter-test',
            'com.h2database:h2'
    )
}

sourceSets {
    intTest {
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
    }
}

configurations {
    intTestImplementation.extendsFrom testImplementation
    intTestRuntimeOnly.extendsFrom testRuntimeOnly
}

tasks.withType(Test) {
    useJUnitPlatform()
}

tasks.register('integrationTest', Test)  {
    description = 'Runs integration tests.'
    group = 'verification'

    testClassesDirs = sourceSets.intTest.output.classesDirs
    classpath = sourceSets.intTest.runtimeClasspath
    shouldRunAfter test
}